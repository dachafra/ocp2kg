# Removes all rules that create instances and properties of %CLASS%.

PREFIX rr: <http://www.w3.org/ns/r2rml#>
PREFIX rml: <http://semweb.mmlab.be/ns/rml#>

DELETE { 
    ?triplesmap rr:subjectMap ?subject.
    ?subject ?subject_term ?subject_value .
    ?subject rr:class <%CLASS%> .

    ?triplesmap rr:logicalSource ?logicalSource .
    ?logicalSource ?logicalSource_term ?logicalSource_value .

    #POM
    ?triplesmap rr:predicateObjectMap ?pom.
    ?pom ?predicate_property ?predicate .
    ?predicate ?predicate_term ?predicate_value .
    ?pom ?object_property ?object.
    ?object ?oject_term ?object_value.
 
    #JOINS
    ?triplesmap rr:predicateObjectMap ?pom2.
    ?pom2 ?predicate_property2 ?predicate2 .
    ?predicate2 ?predicate_term2 ?predicate_value2 .
    ?pom2 rr:objectMap ?object2.
    ?object2 rr:parentTriplesMap ?parent_tm .
    ?object2 rr:joinCondition ?join_condition .
    ?join_condition ?conditions ?condition_values .
    
    #DELETION OF JOINS WHERE TM IS USED
    ?parent_triplesMap rr:predicateObjectMap ?parent_pom.
    ?parent_pom rr:objectMap ?parent_object.
    ?parent_object rr:parentTriplesMap ?triplesmap.
    ?parent_object rr:joinConditions ?parent_joinConditions .
    ?parent_joinConditions ?parent_conditions ?parent_conditions_values . 
} 

WHERE{ 
    ?triplesmap rr:subjectMap ?subject.
    ?subject ?subject_term ?subject_value .
    ?subject rr:class <%CLASS%> .

    ?triplesmap rr:logicalSource ?logicalSource .
    ?logicalSource ?logicalSource_term ?logicalSource_value .

    #POM
    OPTIONAL{
       ?triplesmap rr:predicateObjectMap ?pom.
       ?pom rr:predicate|rr:predicateMap ?predicate .
       OPTIONAL {?predicate ?predicate_term ?predicate_value .}
       ?pom rr:objectMap|rr:object ?object.
       OPTIONAL {?object ?oject_term ?object_value.}
    }
    #REFOBJECTMAP
    OPTIONAL{
       ?triplesmap rr:predicateObjectMap ?pom2.
       ?pom2 rr:predicate|rr:predicateMap ?predicate2 .
       OPTIONAL { ?predicate2 ?predicate_term2 ?predicate_value2 .}
       ?pom2 rr:objectMap ?object2.
       ?object2 rr:parentTriplesMap ?parent_tm .
       OPTIONAL {
         ?object2 rr:joinCondition ?join_condition .
         ?join_condition ?conditions ?condition_values .
      }
    }
    #DELETION OF JOINS
    OPTIONAL{
      ?parent_triplesMap rr:predicateObjectMap ?parent_pom.
      ?parent_pom rr:objectMap ?parent_object.
      ?parent_object rr:parentTriplesMap ?triplesmap.
      OPTIONAL {
         ?parent_object rr:joinConditions ?parent_joinConditions .
         ?parent_joinConditions ?parent_conditions ?parent_conditions_values .
      }
    }
}